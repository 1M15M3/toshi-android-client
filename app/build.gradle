apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.3'
    dataBinding.enabled = true

    defaultConfig {
        applicationId "com.tokenbrowser"
        minSdkVersion 16
        targetSdkVersion 24

        versionCode 7
        versionName "0.1"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            multiDexEnabled true
            debuggable true
            minifyEnabled false
        }
        release {
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }

    // Give each APK in the API split a different versionCode and versionName
    project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'x86': 4]
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) * 1100000 + android.defaultConfig.versionCode
        }
    }
}

dependencies {
    compile(
            // Support
            'com.android.support:appcompat-v7:24.2.1',
            'com.android.support:recyclerview-v7:24.2.1',
            //'com.android.support:gridlayout-v7:24.2.1',
            'com.android.support:design:24.2.1',
            'com.google.android.gms:play-services-ads:9.4.0',
            'com.android.support:multidex:1.0.0',
            // Networking
            'com.squareup.retrofit2:retrofit:2.1.0',
            'com.squareup.retrofit2:converter-moshi:2.1.0',
            'com.squareup.retrofit2:adapter-rxjava:2.1.0',
            'com.squareup.okhttp3:logging-interceptor:3.3.1',
            'com.neovisionaries:nv-websocket-client:1.30',
            'com.artemzin.rxjava:proguard-rules:1.1.9.0',
            // Advertising
            'com.supersonic.sdk:mediationsdk:6.4.18@jar',
            'com.supersonic.adapters:adcolonyadapter:2.1.2@jar',
            'com.adcolony:sdk:3.0.4',
            // Reactive
            'io.reactivex:rxandroid:1.2.1',
            // Images
            'de.hdodenhof:circleimageview:2.1.0',
            // Animations / custom views
            'com.github.ybq:Android-SpinKit:1.1.0',
            'com.hbb20:ccp:1.5.1',
            // Reading barcodes
            'com.journeyapps:zxing-android-embedded:3.3.0',
            // Crypto
            'com.madgag.spongycastle:core:1.54.0.0',
            'com.madgag.spongycastle:prov:1.54.0.0',
            'org.mindrot:jbcrypt:0.3m'
    )

    compile fileTree(include: '*.jar', dir: 'libs')

    compile('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
        transitive = true;
    }

    testCompile(
            'junit:junit:4.12',
            'org.hamcrest:hamcrest-library:1.3'
    )
}